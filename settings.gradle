plugins {
	id "com.gradle.develocity" version "3.17.2"
	id "io.spring.ge.conventions" version "0.0.17"
	id "org.gradle.toolchains.foojay-resolver-convention" version "0.7.0"
}

// Spring Framework Subprojects
include 'framework-platform'

include 'spring-core'
include 'spring-core-test'
include 'spring-instrument'
include 'spring-oxm'
include 'spring-context-support'
include 'spring-jdbc'
include 'spring-tx'
include 'spring-jcl'
include 'spring-context'
include 'spring-aop'
include 'spring-beans'
include 'spring-expression'
include 'spring-web'
include 'spring-webmvc'


rootProject.name = "spring"
rootProject.children.each {project ->
	project.buildFileName = "${project.name}.gradle"
}

settings.gradle.projectsLoaded {
	develocity {
		buildScan {
			File buildDir = settings.gradle.rootProject
					.getLayout().getBuildDirectory().getAsFile().get()
			buildDir.mkdirs()
			new File(buildDir, "build-scan-uri.txt").text = "(build scan not generated)"
			buildScanPublished { scan ->
				if (buildDir.exists()) {
					new File(buildDir, "build-scan-uri.txt").text = "${scan.buildScanUri}\n"
				}
			}
		}
	}
}
